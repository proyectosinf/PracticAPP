plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.ksp)
    alias(libs.plugins.compose.compiler)
}

android {
    namespace 'com.mobivery.fct25.app.common'

    compileSdk libs.versions.compileSdk.get() as Integer

    defaultConfig {
        minSdk libs.versions.minSdk.get() as Integer
        targetSdk libs.versions.targetSdk.get() as Integer
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.toVersion(libs.versions.jvm.get())
        targetCompatibility JavaVersion.toVersion(libs.versions.jvm.get())
    }
    kotlinOptions {
        jvmTarget = libs.versions.jvm.get()
    }
    buildFeatures {
        buildConfig = true
        compose true
    }
    flavorDimensions = ["product", "environment"]
    productFlavors {
        apply from: '../build_variants.gradle', to: ext
        ext.flavors.each { name, config ->
            "$name" {
                dimension config.dimension

                if (config.amplitudeKey != null) {
                    buildConfigField "String", "AMPLITUDE_KEY", "\"${config.amplitudeKey}\""
                }

                // App Name
                if (config.appName != null) {
                    resValue "string", "app_name", config.appName
                }
            }
        }
    }

    // Fixes error when running tests ("duplicated 'META-INF' files")
    packagingOptions { resources.excludes.add("META-INF/*") }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            test {
                reports.junitXml.outputLocation.set(file("$rootDir/build/test-results/common"))
            }
        }
    }
}

dependencies {

    implementation project(path: ":domain")
    implementation project(path: ":data")

    // Compose
    implementation platform(libs.compose.bom)
    implementation libs.compose.ui
    implementation libs.bundles.compose
    implementation libs.compose.activity
    implementation libs.compose.icons
    implementation libs.compose.permissions

    // AndroidX
    implementation libs.androidx.material

    // Dagger/Hilt
    implementation libs.hilt.core
    ksp libs.hilt.compiler

    // Amplitude
    implementation libs.tools.amplitude

    // Gson
    implementation libs.tools.gson

    // Custom tabs
    implementation libs.tools.customTab

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation (libs.firebase.analytics) {
        exclude group: 'com.google.android.gms', module : 'play-services-ads-identifier'
    }

    // Coil
    implementation libs.io.coil.compose

    // Debug
    debugImplementation libs.compose.uiTooling

    // Test
    testImplementation libs.bundles.test

}